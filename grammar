# Lines starting with # are comments

# CAPITALIZED_ITEMS are regular expressions

Expression => LogicalXor

LogicalXor => LogicalOr ( 'xor' LogicalOr )*

LogicalOr => LogicalAnd ( 'or' LogicalAnd )*

LogicalAnd => Equality ( 'and' Equality )*

Equality => Comparison ( ( '!=' | '==' ) Comparison )*

Comparison => Term ( ( '>' | '>=' | '<' | '<=' ) Term )*

Term => Factor ( ( '-' | '+' ) Factor )*

Factor => Exponential ( ( '*' | '/' | '//' | '%' ) Exponential )*

Exponential => Unary ( '**' Unary )*

Unary => ( ( '!' | '-' | '+' | '~' ) Unary ) | Primary

Primary => '(' Expression ')' | Literal

Literal => Void | Boolean | Number | STRING

Void => 'void'

Boolean => 'true' | 'false'

Number => INTEGER | FLOAT
