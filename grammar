# Lines starting with # are comments

# CAPITALIZED_ITEMS are regular expressions

Program => Statement*

Statement => VariableDeclaration StatmentEnd | Expression StatmentEnd?

StatmentEnd => ';'

VariableDeclaration =>
	type NAME ( ':=' Expression )? ( ',' NAME ( ':=' Expression )? )*

type => 'int' | 'float' | 'bool' | 'string' | 'void'

Expression => LogicalOr ( 'xor' LogicalOr )*

LogicalOr => LogicalAnd ( 'or' LogicalAnd )*

LogicalAnd => BitwiseXor ( 'and' BitwiseXor )*

BitwiseXor => BitwiseOr ( '^' BitwiseOr )*

BitwiseOr => BitwiseAnd ( '|' BitwiseAnd )*

BitwiseAnd => Equality ( '&' Equality )*

Equality => Comparison ( ( '!=' | '==' ) Comparison )*

Comparison => Shift ( ( '>' | '>=' | '<' | '<=' ) Shift )*

Shift => Term ( ( '>>' | '<<' ) Term )*

Term => Factor ( ( '-' | '+' ) Factor )*

Factor => Exponential ( ( '*' | '/' | '//' | '%' ) Exponential )*

Exponential => Unary ( '**' Unary )*

Unary => ( ( '!' | '-' | '+' | '~' ) Unary ) | Primary

Primary => Block | Group | Literal

Block => '{' Statement* '}'

Group => '(' Expression ')'

Literal => NAME | Void | Boolean | Number | STRING

Void => 'void'

Boolean => 'true' | 'false'

Number => INTEGER | FLOAT
