# Lines starting with # are comments

# CAPITALIZED_ITEMS are regular expressions

VariableDeclaration =>
	type NAME ( ':=' Expression )? ( ',' NAME ( ':=' Expression )? )* End

type => 'int' | 'float' | 'bool' | 'string' | 'void'

End => ';' | '\n'

Expression => LogicalXor

LogicalXor => LogicalOr ( 'xor' LogicalOr )*

LogicalOr => LogicalAnd ( 'or' LogicalAnd )*

LogicalAnd => BitwiseXor ( 'and' BitwiseXor )*

BitwiseXor => BitwiseOr ( '^' BitwiseOr )*

BitwiseOr => BitwiseAnd ( '|' BitwiseAnd )*

BitwiseAnd => Equality ( '&' Equality )*

Equality => Comparison ( ( '!=' | '==' ) Comparison )*

Comparison => Shift ( ( '>' | '>=' | '<' | '<=' ) Shift )*

Shift => Term ( ( '>>' | '<<' ) Term )*

Term => Factor ( ( '-' | '+' ) Factor )*

Factor => Exponential ( ( '*' | '/' | '//' | '%' ) Exponential )*

Exponential => Unary ( '**' Unary )*

Unary => ( ( '!' | '-' | '+' | '~' ) Unary ) | Primary

Primary => '(' Expression ')' | Literal

Literal => NAME | Void | Boolean | Number | STRING

Void => 'void'

Boolean => 'true' | 'false'

Number => INTEGER | FLOAT
